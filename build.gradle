apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.xe.xecd'
archivesBaseName = project.name
version = '0.1.0'

description = "XE Currency Data API Client"

def ossrhUsername = project.findProperty('ossrhUsername') ?: null;
def ossrhPassword = project.findProperty('ossrhPassword') ?: null;

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required ossrhUsername != null && ossrhPassword != null
    sign configurations.archives
}

dependencies {
    implementation 'org.apache.httpcomponents:httpclient:[4.5.1,5.0)'
    implementation 'org.apache.httpcomponents:httpcore:[4.4.3,5.0)'
    implementation 'com.google.code.gson:gson:[2.3.1,3.0)'
    implementation 'org.slf4j:slf4j-api:[1.3.0, 1.8.0-alpha0)'
    implementation 'junit:junit:[4.12,5.0)'
    
    testImplementation 'ch.qos.logback:logback-classic:[1.1.3,2.0)'
    testImplementation 'joda-time:joda-time:[2.7,3.0)'
    testImplementation 'org.mockito:mockito-all:[1.10.19,2.0)'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name project.name
        packaging 'jar'
        // optionally artifactId can be defined here 
        description project.description
        url 'https://github.com/XenonLab/xecd-rates-client-java'

        scm {
          connection 'scm:git:git://github.com/XenonLab/xecd-rates-client-java.git'
          developerConnection 'scm:git:ssh://github.com:XenonLab/xecd-rates-client-java.git'
          url 'https://github.com/XenonLab/xecd-rates-client-java/tree/master'
        }

        licenses {
          license {
            name 'Mozilla Public License Version 2.0'
            url 'https://www.mozilla.org/media/MPL/2.0/index.815ca599c9df.txt'
          }
        }

        developers {
          developer {
            name 'XE.com Inc. Development Team'
          }
        }
      }
    }
  }
}
